?XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
? PROGRAMME TD3-TD4 ECONOMETRIE DES SERIES TEMPORELLES                                      
?XXXXXXXXXXXXXXXXX
NAME coint;
FREQ Q;
SMPL 1963:1 2021:1;

TREND TIME;	
?XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
?                               ***********************
?                               * LECTURE DES DONNEES *
?                               ***********************

LOAD(FILE='Data1963-2021.XLS',format=EXCEL) ;
SHOW series;
STOP;
?XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
?                         ******************************
?                         * CONSTRUCTION DES VARIABLES *
?                         ******************************
?          ATTENTION: NE RIEN MODIFIER DANS CETTE PARTIE
?          *********************************************
?GENR lsmic=log(smic);
GENR LPqe = log(Pqe);
GENR LQePqe = log(QePqe);
GENR LPy = log(Py);
GENR LPc = log(Pc);
GENR LPin = log(Pinv);
GENR LPm = log(Pm);
GENR LPx = log(Px);
GENR LYPy = log(YPy);
GENR LCPc = log(CPc);
GENR LINVP = log(INVP);
GENR LMPm = log(MPm);
GENR LXPx = log(XPx);
GENR LW = log(W);
GENR LH = log(H);
GENR LN = log(L);
GENR LGpg = log(Gpg);
GENR lpg=log(pg);
GENR lpdi=log(pdi);
GENR lrd=log(rd);
GENR ltcot=log(tcot);
GENR LuPRES = log(1.0-TPRES/1000);
GENR LuCoT = log(1.0-TCot/1000);
GENR LCSU = LW + LN + LH - LYPy - LuCot;
GENR LPyPm = LPy - LPm;
GENR LPinPy = LPin - LPy;
GENR LCTSRL = LW + LH - LuCot - LPY;
GENR BC = Px*XPX - PM*MPM;
GENR LNK = LN - log(K);
GENR a = ypy/l;
GENR LA = log(a);
GENR lac=la-ltcot;
GENR lwpc=lw-lpc;
GENR lwtpy=lw+ltcot-lpy;
GENR lwta=lw+ltcot-la;
GENR LPxPqe = LPx - LPqe;

?                       *************************************
?                       * CALCUL DU COUT D'USAGE DU CAPITAL *
?                       *************************************
SMPL 1963:2 2021:1;
GENR DLPY = LPY-lPY(-1);                                                        
SMPL 1964:2 2021:1;
GENR DT = .02 + .1*(.2*LPYPM + .4*LPYPM(-1) + .2*LPYPM(-2) + .1*LPYPM(-3)
	  + .1*LPYPM(-4));
SMPL 1963:1 64:1;
GENR DT = DT(1964:2);
SMPL 1964:2 2021:1;
GENR PP = .2*DLPY + .4*DLPY(-1) + .2*DLPY(-2) + .1*DLPY(-3) + .1*DLPY(-4);
SMPL 1963:1 1964:1;
GENR PP = PP(1964:2);
smpl 1963:1 2021:1;
GENR LRPD = log(tr - 100*PP + 100*DT);
GENR LVC = LPinPy + LRPD;
GENR LWLVC = LW + LH - LuCot - LVC - LPY;
smpl 1963:2 2021:1;
GENR IK = INVP/K(-1);
GENR lik=log(ik);
?**************************************************
smpl 1963:2 2021:1;
GENR dlwtpy=lwtpy-lwtpy(-1);
GENR dlnk=lnk-lnk(-1);
GENR dlpy=log(py/py(-1));
GENR dduc=duc-duc(-1);
GENR dlcsu=lcsu-lcsu(-1);
GENR dlypy=log(ypy/ypy(-1));
GENR dlpinpy=lpinpy-lpinpy(-1);
GENR dlrpd=lrpd-lrpd(-1);
GENR dln=log(l/l(-1));
GENR dlwlvc=lwlvc-lwlvc(-1);
GENR dlh=log(h/h(-1));
GENR dlvc=lvc-lvc(-1);
GENR dlnk=lnk-lnk(-1);
GENR dlcpc=log(cpc/cpc(-1));
GENR dlrd=lrd-lrd(-1);
GENR dlxpx=log(xpx/xpx(-1));
GENR dlqepqe=log(qepqe/qepqe(-1));
GENR dlpxpqe=lpxpqe-lpxpqe(-1);
GENR dlmpm=log(mpm/mpm(-1));
GENR dlpypm=lpypm-lpypm(-1);
GENR dlpx=log(px/px(-1));
GENR dlpqe=log(pqe/pqe(-1));
GENR dlinvp=log(invp/invp(-1));
GENR dlg=log(gpg/gpg(-1));
GENR dtcho=tcho-tcho(-1);
GENR dlwpc=lwpc-lwpc(-1);
GENR dla=la-la(-1);
GENR dlpc=lpc-lpc(-1);
GENR dlpm=lpm-lpm(-1);
GENR dlw=lw-lw(-1);
GENR dtr=tr-tr(-1);
GENR dlwta=lwta-lwta(-1);
GENR dlpdi=lpdi-lpdi(-1);
GENR dlac=lac-lac(-1);

smpl 1963:3 2021:1;
GENR ddlpy=dlpy-dlpy(-1);
GENR dik=ik-ik(-1);
GENR dlik=lik-lik(-1);
?goto 10;
?XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
?                                DEBUT DES TD
?                               ZONE DE TRAVAIL
?XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

?XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
?     TD N°4 : LA COINTEGRATION
?XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

? VARIABLE LCPC
? ETAPE N°1: RECHERCHE DE SAISONNALITE

SMPL 63:1 2021:1; 
BJIDENT(NLAG=200,NOPLOT,PLOTAC,NDIFF=0,NSDIFF=0,NSPAN=0) lcpc;	? pas de saisonnalité

PLOTAC;  ? call Proc defined below

STOP;
? ETAPE N°2: RECHERCHE DE cste et trend

OLSQ lcpc c time;
FORM(print) eq;	  ? On utlise le test DF3

STOP;
? ETAPE N°3: RECHERCHE DE RACINE UNITAIRE (ordre de stationnarité)

SMPL 1963:1 2021:1;
COINT(all,const,trend) lcpc;  ? PV > .05 donc lcpc est au moins I(1)
SMPL 1963:2 2021:1;
COINT(all,const,trend) dlcpc; ? PV < .05 donc lcpc est I(1)
SMPL 1963:3 2021:1;
COINT(all,const,trend) ddlcpc; ? PV < .05 donc lcpc est I(1)

stop;
? on doit faire de même pour LRD TCHO et LPC






? ETAPE N°4 : RECHERCHE DE VECTEUR COINTEGRANT 4*4
                                                          
SMPL 1963:1 2021:1 ;                    
coint(allord,const,trend) lcpc lrd tcho lpc;   ? PV < .05 donc les séries sont cointégrées

stop;

? ETAPE N°5 :    Estimation en deux etapes de Engle et Granger

? Etape 1 de EG
? -------------
smpl 1963:1 2021:1;
regopt(pvprint,stars) all;
olsq lcpc lrd tcho lpc c time;                  


STOP;
? Etape 2 de EG
? -------------
smpl 64:3 2021:1;
olsq dlcpc dlcpc(-1) dlcpc(-2) dlcpc(-3) dlcpc(-4) dlcpc(-5)
           dlrd dlrd(-1) dlrd(-2) dlrd(-3) dlrd(-4)  dlrd(-5)
		   dtcho dtcho(-1) dtcho(-2) dtcho(-3) dtcho(-4) dtcho(-5)
		   dlpc dlpc(-1) dlpc(-2) dlpc(-3) dlpc(-4) dlpc(-5)
       	 c @res(-1);

? Nous obtenons :








?XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
Proc PlotAC;
 local smpsav frqsav ub nac r acsq ub95% lb95%;
 copy @smpl smpsav;
 copy @freq frqsav;
 mat nac=nrow(@ac);
 supres smpl;
 freq n;
 smpl 1,nac;
 unmake @ac r;
 ? constant SEs
 ?set ub=cnormi(1-.05/2)/sqrt(@nob);
 ? Bartlett SEs
 acsq = 0;
 smpl 2,nac;
 acsq = acsq(-1) + R(-1)**2;
 smpl 1,nac;
 ub = cnormi(1-.05/2)*sqrt((1+2*acsq)/nac);
 ub95%=ub;
 lb95%=-ub;
 plot(preview,origin,title="graphique lfp") R ub95% lb95%;
 freq frqsav;
 smpl smpsav;
endproc;
?XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

END;










